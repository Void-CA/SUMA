// Reglas vitales del sistema
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

// Entrada principal
optimization_block = { "optimization" ~ "{" ~ header ~ constraints_section? ~ "}" }

// Cabecera: Dirección y Función Objetivo
header = { direction ~ expression }
direction = { "maximize" | "minimize" | "max" | "min" }

// Restricciones
constraints_section = { "constraints" ~ "{" ~ constraint+ ~ "}" }
constraint = { expression ~ relation ~ expression }
relation = { "<=" | ">=" | "=" | "<" | ">" }

// --- Aritmética (Mapeable a Expr) ---
// Usamos reglas estándar para precedencia
expression = { term ~ (add_op ~ term)* }
term       = { factor ~ (mul_op ~ factor)* }
factor     = { neg_op? ~ atom }
atom       = _{ number | variable | "(" ~ expression ~ ")" }

add_op = { "+" | "-" }
mul_op = { "*" | "/" }
neg_op = { "-" }

number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
variable = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }