// Reglas de sistema
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT    = _{ "//" ~ (!"\n" ~ ANY)* }

// ENTRY POINT: Ahora solo aceptamos definiciones
// El dispatcher solo nos llamará si el texto empieza con "Optimization"
optimization_block = { SOI ~ definition ~ EOI }

// --- 1. DEFINICIÓN DEL MODELO ---
definition = { ^"Optimization" ~ model_id ~ "{" ~ header ~ constraints_section? ~ "}" }

model_id = { string_lit }

header = { direction ~ expression }
direction = { "maximize" | "minimize" | "max" | "min" }

constraints_section = { "constraints" ~ "{" ~ constraint+ ~ "}" }
constraint = { expression ~ relation ~ expression }
relation = { "<=" | ">=" | "=" | "<" | ">" }

// --- ARITMÉTICA Y UTILIDADES ---
expression = { term ~ (add_op ~ term)* }
term       = { factor ~ (mul_op ~ factor)* }
factor     = { neg_op? ~ atom }
atom       = _{ number | variable | "(" ~ expression ~ ")" }

add_op = { "+" | "-" }
mul_op = { "*" | "/" }
neg_op = { "-" }

number     = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
variable   = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
string_lit = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }