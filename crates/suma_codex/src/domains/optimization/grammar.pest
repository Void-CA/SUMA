// Reglas de sistema
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

// BLOQUE PRINCIPAL
// Ahora acepta un "model_id" opcional antes de la llave
optimization_block = { "Optimization" ~ model_id? ~ "{" ~ header ~ constraints_section? ~ "}" }

// Identificador del modelo (ej: "MiProblema")
model_id = { string_lit }

// Cabecera
header = { direction ~ expression }
direction = { "maximize" | "minimize" | "max" | "min" }

// Restricciones
constraints_section = { "constraints" ~ "{" ~ constraint+ ~ "}" }
constraint = { expression ~ relation ~ expression }
relation = { "<=" | ">=" | "=" | "<" | ">" }

// Aritmética
expression = { term ~ (add_op ~ term)* }
term       = { factor ~ (mul_op ~ factor)* }
factor     = { neg_op? ~ atom }
atom       = _{ number | variable | "(" ~ expression ~ ")" }

add_op = { "+" | "-" }
mul_op = { "*" | "/" }
neg_op = { "-" }

number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
variable = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

// Utilidad de Strings (Copiada de LinearAlgebra o común)
string_lit = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }